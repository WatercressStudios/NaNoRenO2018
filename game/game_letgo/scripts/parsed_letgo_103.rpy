label letgo_103:
    scene letgo townsquare cafe outside with dissolve

    "We leave the cafe, talking lightly and generally enjoying each other’s company."

    scene letgo road long with dissolve

    "As we make our way along, we get close to the park we used to spend time in when we were younger."

    scene letgo park lake path with dissolve
    play ambience "game_letgo/ambience/Park Day.ogg" fadeout 2.0 fadein 2.0

    show eli normal1 with dissolve:
        align (0.05,1.0)
    show may adore1 with dissolve:
        align (0.35,1.0)

    voice "C-103-1.mp3" #Elijah (Michael Potok)
    eli "Hey, there’s our park."

    show may challenge1 with dissolve

    voice "C-103-2.mp3" #Maya (shiena)
    may "Oh! Let’s go play on the swings!"

    show may:
        ease 3.0 align (1.0,1.0)
    show eli worried1 with dissolve

    voice "C-103-3.mp3" #Elijah (Michael Potok)
    eli "Wait, but... it’s getting late! And cold!"

    "She’s already jogging to them when she looks over her shoulder to yell at me."

    voice "C-103-4.mp3" #Maya (shiena)
    may "Come on! It’ll be fun, just like when we were kids!"

    show eli normal1 with dissolve:
        ease 3.0 align (0.85,1.0)

    "I hurry to catch up with her, my panting visible in the cold."

    scene letgo park swing with dissolve
    show may adore1 with dissolve:
        align (0.25,1.0)
    show eli sad1 with dissolve:
        align (0.6,1.0)

    "She brushes a little snow off a swing to sit."

    "In the fading light of sunset, her hair shines and she has a luminescence about her."

    voice "C-103-5.mp3" #Maya (shiena)
    may "What are you staring at? Start pushing!"

    "She kicks her feet excitedly and I can’t help but laugh."

    show eli happy1 with dissolve

    voice "C-103-6.mp3" #Elijah (Michael Potok)
    eli "Of course."

    show eli:
        ease 1.0 align (0.45,1.0)
        ease 1.5 align (0.55,1.0)
    pause 1.0
    show may:
        ease 1.5 align (0.35,1.0)

    pause 1.5
    show eli:
        ease 0.4 align (0.6,1.0)
    show may:
        ease 1.5 align (0.25,1.0)
        pause 0.1
        ease 1.5 align (0.35,1.0)
        pause 0.1
        repeat
    "I stand behind her and grab the chains. Leaning back, I pull her up high and then let go."

    voice "C-101-may-laugh.mp3"
    "She lets out a little shout, laughing and swinging."

    "Her hair blows in the wind, the scarf flying behind her when she moves forward."

    voice "C-103-7.mp3" #Maya (shiena)
    may "You gotta swing with me! Come on!"

    voice "C-103-8.mp3" #Elijah (Michael Potok)
    eli "I’m pretty sure my legs are too long. My feet will scrape the ground."

    voice "C-103-9.mp3" #Maya (shiena)
    may "Well, you gotta try!"

    voice "C-103-10.mp3" #Elijah (Michael Potok)
    eli "Alright, alright, I’m going."

    show eli:
        ease 1.5 align (0.55,1.0)
        pause 0.15
        ease 1.5 align (0.65,1.0)
        pause 0.15
        repeat

    "Sitting on the swing beside her, I bend my knees back and forth a bit; eventually, I gain a little momentum."

    voice "C-103-11.mp3" #Maya (shiena)
    may "Is that all you got? Remember when we used to see who could swing the highest? But we were both going too hard to tell who was actually the winner."

    voice "C-103-12.mp3" #Elijah (Michael Potok)
    eli "Yeah, there were a couple times where we were swinging so high I was worried about sliding off! It was worth it to beat you though."

    "She slows down to my level and I catch her eye with a smirk."
    show may challenge1 with dissolve

    voice "C-103-13.mp3" #Maya (shiena)
    may "Yeah, yeah, okay. Just because your legs got longer doesn’t mean your ego should grow too. I won sometimes!"

    show eli goofy1 with dissolve

    voice "C-103-14.mp3" #Elijah (Michael Potok)
    eli "And who do you think let that happen?"

    show may shout1 with dissolve

    voice "C-103-15.mp3" #Maya (shiena)
    may "What! That’s so unfair, ya know. All I was trying to do was impress you."

    show eli worried1 with dissolve

    voice "C-103-16.mp3" #Elijah (Michael Potok)
    eli "Impress {i}me?{/i} I didn’t get that vibe at all."

    show may cheeky1 with dissolve

    voice "C-103-17.mp3" #Maya (shiena)
    may "Well, I think we’ve established you’re a little thick-headed."

    "Is this flirting?"

    "I should go along."

    "Right?"

    show eli cool1 with dissolve

    voice "C-103-18.mp3" #Elijah (Michael Potok)
    eli "Thick-headed? What? I’m sorry, I couldn’t quite understand what you said."

    voice "C-103-19.mp3" #Maya (shiena)
    may "See? You just can’t keep up."

    voice "C-103-20.mp3" #Elijah (Michael Potok)
    eli "I just didn’t notice, I guess - which is ridiculous, since I was always watching you."

    show eli sad1 with dissolve

    voice "C-103-21.mp3" #Elijah (Michael Potok)
    eli "Well, n-not watching you! You were just always on my mind is all! You’ve {i}always{/i} been on my mind..."

    "Don’t make it weird! You’re okay..."

    "This is nice, just enjoy the time together."

    "Live in the moment; everything is going to be just… great. It’ll be great!"

    show may:
        ease 0.5 align (0.4,1.0)
        pause 0.3
    show eli:
        ease 0.5 align (0.6,1.0)

    "She reaches over and pulls on the chain of my swing."

    voice "C-103-22.mp3" #Maya (shiena)
    may "You’re on my mind too."

    voice "C-103-23.mp3" #Maya (shiena)
    may "Wanna walk over to the lake with me? Watch the end of the sunset?"

    "It’s so captivating to look into her eyes."

    voice "C-103-24.mp3" #Elijah (Michael Potok)
    eli "Yeah, that would be nice."

    play sound "game_letgo/sfx/Snow Footsteps.ogg"
    scene letgo park trees sunset with dissolve

    "We walk over to the lake - and this time I actually take her hand. She slowly intertwines her fingers with mine and squeezes a little. It feels comfortable - {i}right{/i} - even through our gloves."

    "The short walk has the sun setting and the brightest stars coming into focus instead."

    scene letgo park lake sunset with dissolve

    "We eventually arrive, and she finds a fallen tree to sit on."

    show eli normal1 with dissolve:
        align (0.67,1.0)
    show may adore1 with dissolve:
        align (0.82,1.0)

    "I join her and we watch the water in silence for a while; it’s a calming silence, though - like this is how things are supposed to be."

    voice "C-103-25.mp3" #Maya (shiena)
    may "The stars are coming out. They look nice."

    voice "C-103-26.mp3" #Elijah (Michael Potok)
    eli "Yeah, they’re pretty clear tonight."

    show may sheepish2 close with dissolve

    voice "C-103-27.mp3" #Maya (shiena)
    may "Eli, have you ever made a wish on a shooting star?"

    show eli worried1 with dissolve

    voice "C-103-28.mp3" #Elijah (Michael Potok)
    eli "No; the only wishes I’ve made were on birthday candles and wishing wells."

    voice "C-103-29.mp3" #Maya (shiena)
    may "I feel like making one tonight."

    show eli smile2 with dissolve

    voice "C-103-30.mp3" #Elijah (Michael Potok)
    eli "Well, birthday candles only work if it’s your birthday - and I don’t know of any wishing wells around here. But maybe we could throw something into the lake? I can find us some rocks or some leaves or--?"

    show may sheepish2 with dissolve

    voice "C-103-31.mp3" #Maya (shiena)
    may "Yeah, let’s do that."

    "She smiles warmly at me. I wonder what she would want to wish for."

    "We gather a few rocks and stand near the water’s edge, ready to throw."

    show eli goofy1 with dissolve

    voice "C-103-32.mp3" #Elijah (Michael Potok)
    eli "Well, you have to go first since it was your idea."

    voice "C-103-33.mp3" #Maya (shiena)
    may "I don’t know what to wish for!"

    show eli worried1 with dissolve

    voice "C-103-34.mp3" #Elijah (Michael Potok)
    eli "But you’re the one who wanted to do that!"

    show may aww1 with dissolve

    voice "C-103-35.mp3" #Maya (shiena)
    may "I guess I just wanted to feel the magic, ya know? Like that’s the only thing that could make this night complete."

    show eli sad1 with dissolve

    voice "C-103-36.mp3" #Elijah (Michael Potok)
    eli "Well, I don’t want to go first; I’m not ready yet either!"

    show may proud2 with dissolve

    voice "C-103-37.mp3" #Maya (shiena)
    may "How about we throw all of our rocks at the same time? Make all of our wishes at once, like a big wish made of little ones."

    show eli happy1 with dissolve

    voice "C-103-38.mp3" #Elijah (Michael Potok)
    eli "This is a little silly, you know. Maybe if these were coins and this was a well, or a fountain, or something."

    voice "C-103-39.mp3" #Maya (shiena)
    may "Would you just close our eyes and make your wishes?"

    voice "C-103-40.mp3" #Elijah (Michael Potok)
    eli "Okay, okay, let’s go."

    voice "C-103-41.mp3" #Maya (shiena)
    may "Ready? Three… Two… One...!"

    stop ambience fadeout 5.0
    show eli happy1 close with dissolve
    show may happy2 close with dissolve

    scene black with Dissolve(1.0)

    "We both hurl a fistfull of pebbles into the water."

    "The splashes echo into the quiet dusk."

    stop music fadeout 3.0

    "I wish for this to last forever..."

    jump letgo_104